syntax = syntax_rule+ ;
syntax_rule = meta_identifier '=' definitions_list ';' ;
definitions_list = single_definition ('|' single_definition)* ;
single_definition = term (',' term)* ;
term = factor ('-' exception)? ;
exception = factor ;
factor = (integer '*')? primary ;
primary = optional_sequence ;
primary = repeated_sequence ;
primary = special_sequence ;
primary = grouped_sequence ;
primary = meta_identifier ;
primary = terminal_string ;
primary = empty ;
empty = ;
optional_sequence = '[' definitions_list ']' ;
repeated_sequence = '{' definitions_list '}' ;
grouped_sequence = '(' definitions_list ')' ;
terminal_string = '’' character+ '’' ;
terminal_string = '"' character+ '"' ;
meta_identifier = letter (letter | decimal_digit)* ;
integer = decimal_digit+ ;
special_sequence = '?' character* '?' ;
comment = '(*' comment_symbol* '*)' ;
comment_symbol = comment ;
comment_symbol = terminal_string ;
comment_symbol = special_sequence ;
comment_symbol = character ;
