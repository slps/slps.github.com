SYNTAX:
        SYNTAX_RULE+
SYNTAX_RULE:
        META_IDENTIFIER "=" DEFINITIONS_LIST "."
DEFINITIONS_LIST:
        SINGLE_DEFINITION ("/" SINGLE_DEFINITION)*
SINGLE_DEFINITION:
        TERM ("," TERM)*
TERM:
        FACTOR ("-" EXCEPTION)?
EXCEPTION:
        FACTOR
FACTOR:
        (INTEGER "*")? PRIMARY
PRIMARY:
        OPTIONAL_SEQUENCE
        REPEATED_SEQUENCE
        SPECIAL_SEQUENCE
        GROUPED_SEQUENCE
        META_IDENTIFIER
        TERMINAL
        EMPTY
EMPTY:
        EPSILON
OPTIONAL_SEQUENCE:
        "(/" DEFINITIONS_LIST "/)"
REPEATED_SEQUENCE:
        "(:" DEFINITIONS_LIST ":)"
GROUPED_SEQUENCE:
        "(" DEFINITIONS_LIST ")"
TERMINAL:
        "’" CHARACTER+ "’"
        """ CHARACTER+ """
META_IDENTIFIER:
        LETTER (LETTER | DIGIT)*
INTEGER:
        DIGIT+
SPECIAL_SEQUENCE:
        "?" CHARACTER* "?"
COMMENT:
        "(*" COMMENT_SYMBOL* "*)"
COMMENT_SYMBOL:
        COMMENT
        TERMINAL
        SPECIAL_SEQUENCE
        CHARACTER
